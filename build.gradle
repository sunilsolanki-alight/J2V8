apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.eclipsesource.j2v8'
archivesBaseName = 'j2v8'
version = System.getenv('J2V8_FULL_VERSION')

ext."signing.secretKeyRingFile" = '/.gnupg/secring.gpg'
ext."signing.password" = System.getenv("KEYSTORE_PASSWORD")
ext."signing.keyId" = System.getenv("KEY_ID")

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    }
}

apply plugin: 'spoon'

repositories {
    google()
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    //testImplementation 'org.mockito:mockito-all:2.6.3'
    testImplementation 'org.mockito:mockito-core:4.11.0'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-android:4.11.0' // https://jeroenmols.com/blog/2017/01/17/mockitoandroid/
    androidTestImplementation 'androidx.annotation:annotation:1.9.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
}

android {
    namespace 'com.eclipsesource.v8'
    compileSdk 35
    
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['src/test']
        }

        // androidTest.setRoot('src/test')
    }

}

// see: https://github.com/square/spoon
spoon {
    // for debug output
    debug = true

    // To run only specified test classes
    if (project.hasProperty('testClass')) {
        className = project.testClass
    }

    // To run a single method in TestCase
    if (project.hasProperty('testMethod')) {
        methodName = project.testMethod
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact bundleReleaseAar
            }
        }
    }
}
